// simulate sequences with actual nucleotides

initialize() {
    defineConstant("L", 50000);
    initializeSLiMOptions(nucleotideBased=T);
    initializeAncestralNucleotides( randomNucleotides(L) );
    initializeMutationTypeNuc("m1", 0.5, "f", 0.0);
    // initializeMutationRate(2.5e-6);
    initializeGenomicElementType("g1", m1, 1.0, mmJukesCantor(8.3e-7)); //  (2.5/3~=0.83)e-8 scaled by 100
    initializeGenomicElement(g1, 0, L-1);
    initializeRecombinationRate(1e-6); // 1e-8 scaled by 100
}
1 {
    // defineConstant("simID",getSeed()); // get seed in case of re-run
    sim.addSubpop("p1", 100); // 1e4 scaled by 100
}

// end of burn-in, 8mya gorilla split off
4500 late(){ 
    sim.addSubpopSplit("p2",100,p1);
}


// 5mya chimpanzees split off
6000 late(){
    sim.addSubpopSplit("p3",100,p1) ;
}

// sample
8500 late() {
    sam = p1.sampleIndividuals(25).genomes ; // human
    sam = c(sam, p3.sampleIndividuals(25).genomes ) ; // chimp
    sam = c(sam, p2.sampleIndividuals(1).genomes ) ; // gorilla
    cat(asString('\n// output population samples\n'));
    sam.outputVCF(simplifyNucleotides=F);
    // p1.outputMSSample(50); // human
    // p3.outputMSSample(50); // chimp
    // p2.outputMSSample(1);
}
